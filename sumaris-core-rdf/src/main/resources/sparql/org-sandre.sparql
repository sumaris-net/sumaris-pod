PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX inc1: <http://id.eaufrance.fr/ddd/INC/1.0/>
PREFIX inc: <http://id.eaufrance.fr/ddd/INC/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX s: <http://schema.org/>
CONSTRUCT {
  ?sub rdfs:label ?name ;
    gr:name ?name ;
    gr:description ?name ;
    rdf:type foaf:Organization ;
    rdf:type org:Organization ;
    org:identifier ?identifier ;
    dc:identifier ?identifier ;
    dc:title ?name ;
    dc:created ?created ;
    dc:modified ?modified ;
    dcterms:created ?created ;
    dcterms:modified ?modified ;
    owl:sameAs ?match ;
    rdfs:seeAlso ?seeAlso ;
    org:hasprimarySite _:site ;
    s:address _:site .
  _:site rdf:type org:Site ;
    rdf:type gr:Location ;
    rdf:type s:PostalAddress ;
    org:siteAddress ?siteAddress ;
    s:streetAddress ?streetAddress ;
    s:postalCode ?postalCode ;
    s:addressLocality ?city ;
    s:addressCountry ?addressCountry .
}
WHERE {
  ?sub rdf:type inc:Interlocuteur ;
     rdfs:label ?name ;
     inc1:CdInterlocuteur ?identifier ;
     inc1:DateCreInterlocuteur ?created ;
     inc1:DateMAJInterlocuteur ?modified ;
     inc1:StInterlocuteur ?validityStatusUri ;
     inc1:EtatInterlocuteur ?statusUri .
  OPTIONAL {
    ?sub inc1:MnInterlocuteur ?shortName .
  }
  OPTIONAL {
    ?sub inc1:AdresseInterlocuteur ?bnAddress .
    OPTIONAL { ?bnAddress inc1:Compl2Adresse ?addressCompl2 . }
    OPTIONAL { ?bnAddress inc1:Compl3Adresse ?addressCompl3 . }
    OPTIONAL { ?bnAddress inc1:NumLbVoieAdresse ?addressRoad . }
    OPTIONAL { ?bnAddress inc1:LgAcheAdresse ?postalCodeAndCity . }
    BIND(
      REPLACE(REPLACE(
        CONCAT(?addressCompl2, '|', ?addressCompl3, '|', ?addressRoad),
          '^[|]+', '', 'i'),
        '[|]+', ', ', 'i')
      as ?streetAddress
	)
    BIND(
      REPLACE(?postalCodeAndCity, '[A-Z ]+', '', 'i') as ?postalCode
	)
    BIND(
      REPLACE(?postalCodeAndCity, '^[0-9 ]+', '', 'i') as ?city
	)
    BIND(
      REPLACE(REPLACE(
        CONCAT(?streetAddress, '|', ?postalCodeAndCity),
          '^[|]+', '', 'i'),
        '[|]+', ', ', 'i')
      as ?siteAddress
	)
  }
  OPTIONAL {
    ?sub inc1:PaysInterlocuteur _:country .
  	_:country inc1:CdPays ?addressCountry .
  }
  OPTIONAL {
    ?sub skos:exactMatch|owl:sameAs ?match .
  }
  OPTIONAL {
    ?sub rdf:seeAlso|rdfs:seeAlso|foaf:page ?seeAlso .
  }
}